//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Update Hat Maker By: ==================================================
//= Boy
//===== Current Version: =====================================
//= 1.4a
//===== Compatible With: =====================================
//= rAthena SVN r15340+
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//============================================================
					
moc_para01,36,38,4	script	Kielle 	646,{
function Add; function Chk; function Slot; function A_An;

	if(.Shops$ != "") set .@i,1;
	else {
		set .@menu$,"";
		for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1)
			set .@menu$, .@menu$+.Shops$[.@i]+":";
		set .@i, select(.@menu$); }
	dispbottom "Select one item at a time.";
	callshop "hatmakerhigher"+.@i,1;
	npcshopattach "hatmakerhigher"+.@i;
	end;

function Add {
	if (getitemname(getarg(1))=="null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped)."; return; }
	for(set .@n,5; .@n<127; set .@n,.@n+2) {
		if (!getarg(.@n,0)) break;
		if (getitemname(getarg(.@n))=="null") {
			debugmes "Quest requirement #"+getarg(.@n)+" invalid (skipped) on Quest ItemID "+ getarg(1) +"."; return; } }
	for(set .@i,2; .@i<.@n; set .@i,.@i+1)
		set getd(".q_"+getarg(1)+"["+(.@i-2)+"]"), getarg(.@i);
	npcshopadditem "hatmakerhigher"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return; }
function Chk {
	if (getarg(0)<getarg(1)) { set @qe0,1; return "^FF0000"; }
	else return "^00FF00"; }
function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot){
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),11)>0) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$; } }
function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0); }

OnBuyItem:
	set .@q[0],@bought_nameid;
	copyarray .@q[1],getd(".q_"+@bought_nameid+"[0]"),getarraysize(getd(".q_"+@bought_nameid+"[0]"));
	if (!.@q[1]) { message strcharinfo(0),"An error has occurred."; end; }
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[1]>1)?.@q[1]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	if (.@q[2]) mes " > "+Chk(Zeny,.@q[2])+.@q[2]+" Zeny^000000";
	if (.@q[3]) mes " > "+Chk(getd(.Points$[0]),.@q[3])+.@q[3]+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+.@q[3]+")^000000";
	if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1])+((.DisplayID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+.@q[.@i+1]+")^000000";
	next;
	set @qe1, getiteminfo(.@q[0],5); set @qe2, getiteminfo(.@q[0],11);
	addtimer 1000, strnpcinfo(1)+"::OnEnd";
	while(1){
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((((@qe1&1) || (@qe1&256) || (@qe1&512)) && @qe2>0 && !@qe6)?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
			case 1:
				if (@qe0) { 
					mes "[Quest Shop]";
					mes "You're missing one or more quest requirements.";
					close; }
				if (!checkweight(.@q[0],.@q[1])) {
					mes "[Quest Shop]";
					mes "^FF0000You need "+(((.@q[1]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
					close; }
				if (.@q[2]) set Zeny, Zeny-.@q[2];
				if (.@q[3]) setd .Points$[0], getd(.Points$[0])-.@q[3];
				if (.@q[4]) for(set .@i,4; .@i<getarraysize(.@q); set .@i,.@i+2)
					delitem .@q[.@i],.@q[.@i+1];
				getitem .@q[0],.@q[1];
				if (.Announce) announce strcharinfo(0)+" has created "+A_An(getitemname(.@q[0]))+"!",0;
				specialeffect2 699;
				close;
			case 2:
				set @qe3, getlook(3); set @qe4, getlook(4); set @qe5, getlook(5);
				if (@qe1&1) atcommand "@changelook 3 "+@qe2;
				if (@qe1&256) atcommand "@changelook 1 "+@qe2;
				if (@qe1&512) atcommand "@changelook 2 "+@qe2;
				set @qe6,1;
				break;
			case 3:
				close; } }
OnEnd:
	if (@qe6) { atcommand "@changelook 3 "+@qe3; atcommand "@changelook 1 "+@qe4; atcommand "@changelook 2 "+@qe5; }
	for(set .@i,0; .@i<7; set .@i,.@i+1) setd "@qe"+.@i,0;
	end;
OnInit:
// --------------------- Config ---------------------
// Custom points, if needed: "<variable>","<name to display>"
	setarray .Points$[0],"#CASHPOINTS","Cash Points";

	set .Announce,1;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .DisplayID,0;	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)

// Shop categories, if needed: "<Shop 1>","<Shop 2>"{,...};
// Duplicate dummy data for any additional shops (bottom of script).
// If no categories, use the second line instead (remove //).

	setarray .Shops$[1],"Upper Hat Quest Items";
	// set .Shops$,"n/a";

// Add(<shop number>,<reward ID>,<reward amount>,<Zeny cost>,<point cost>,
//     <required item ID>,<required item amount>{,...});
// Shop number corresponds with order above (default is 1).
// Note: Do NOT use a reward item more than once!

	//Upper
	Add(1,5766,1,0,0,12040,1,4150,1,923,20,7038,200,35004,4); //Amistr Cap#5766
	Add(1,5137,1,0,0,5141,1,1034,600,7047,200,9027,1,35004,10); // Alice Doll
	Add(1,5531,1,0,0,2527,1,12080,1,7445,150,1037,800,35004,8); //Baby Dragon Hat[1] #5531
	Add(1,5359,1,0,0,5174,1,2284,1,1757,5000,7222,500,35004,15); // Ship Captain Hat
	Add(1,5320,1,0,0,2210,1,714,3,968,10,5064,20,35004,20); // Champion, Wreath
	Add(1,5283,1,0,0,5110,1,5107,1,916,300,917,250,35004,1); //Chick Hat
	Add(1,5431,1,0,0,5283,1,2272,1,949,200,5015,5,35004,2); //Chicken Hat
	Add(1,5417,1,0,0,1964,1,2287,1,7441,10,916,300,35004,3); //Crown Parrots [1]#5417
	Add(1,5529,1,0,0,5017,1,7450,1,7566,2,7005,999,35004,15); //Satanic Bone Helm
	Add(1,5254,1,0,0,5038,1,694,1,5102,1,7023,3,35004,10); //Deviling Hat [1]#5254
	Add(1,5211,1,0,0,5045,1,5083,1,5117,1,2513,1,35004,8); // Dress Hat
	Add(1,5671,1,0,0,5289,1,5253,1,5405,1,5766,1,35004,1); //Drooping Morroc Minion [1]#5671
	Add(1,5405,1,0,0,12040,1,4322,1,5283,1,7063,50,35004,4); //Filir Hat#5405
	Add(1,5380,1,0,0,1256,1,995,10,1023,300,624,5,35004,3); //Fish Head Hat
	Add(1,5269,1,0,0,2254,1,1616,1,7162,200,7063,120,35004,6); // Flying Angel Wing		
	Add(1,5447,1,0,0,4014,1,5023,1,938,500,918,300,35004,4); //Frog Hat
	sleep 10;
	
	Add(1,5528,1,0,0,2249,1,5447,1,7155,180,970,20,35004,6); //Frog King Hat
	Add(1,5660,1,0,0,1971,1,996,10,7115,300,7048,10,35004,3); // Gryphon Hat
	Add(1,5465,1,0,0,5021,1,2247,1,2652,1,978,3,35004,7); //Hat of Fortune #5465
	Add(1,5658,1,0,0,13303,1,994,10,7122,300,9056,1,35004,3); // Imp Hat
	Add(1,5497,1,0,0,1029,1,4411,1,2531,1,7267,400,35004,5); // King Tiger Doll Hat
	Add(1,5372,1,0,0,5058,1,5102,1,7213,200,982,3,35004,5); // Koneko Hat	
	Add(1,5526,1,0,0,5117,1,7165,100,7152,200,950,200,35004,3); // Tanigumi Girl Doll
	Add(1,5555,1,0,0,5065,1,7298,300,1023,150,12001,20,35004,2); // Leaf Cat Hat
	Add(1,5253,1,0,0,12040,1,521,150,7100,150,7194,150,35004,4); // Lif Doll Hat
	Add(1,5138,1,0,0,5027,1,1061,500,7263,100,981,5,35004,6); // Magic Eyes
	Add(1,5420,1,0,0,5421,1,1471,1,7120,555,7451,3,35004,11); //Mask Of Ifrit
	Add(1,5454,1,0,0,5118,1,628,3,919,800,1060,400,35004,6); // Puppy Hat
	sleep 10;
	
	Add(1,5293,1,0,0,4344,1,12395,1,12129,1,520,400,35004,10); // Ramen Hat
	Add(1,5208,1,0,0,1550,1,603,50,1053,32,1097,300,35004,6); // Rideword Hat
	Add(1,5547,1,0,0,7510,1,7293,3,1032,450,7186,450,35004,5); // RJC Katusa Flower
	Add(1,5376,1,0,0,2255,1,13405,1,7340,666,1039,666,35004,15); //Flying Evil Wing
	Add(1,5659,1,0,0,1967,1,997,10,7004,300,1000,20,35004,3); // Sleeper Hat
	Add(1,5761,1,0,0,2504,1,7198,400,7172,200,1015,120,35004,4); //Sloth Hat
	Add(1,5388,1,0,0,4117,1,4037,1,1048,300,5040,1,35004,4); // Snake Head Hat
	Add(1,5611,1,0,0,4220,1,967,300,903,200,979,1,35004,1); //Turtle Hat
	Add(1,5289,1,0,0,12040,1,692,20,688,20,690,20,35004,4); // Vanilmirth Hat
	Add(1,5498,1,0,0,2248,1,2531,1,1034,800,7030,800,35004,6); //Wandering Wolf Helm 
	Add(1,5360,1,0,0,5057,1,4157,1,7511,99,983,2,35004,3); // Hyuke's Black Cat Ears
	Add(1,5367,1,0,0,5168,1,5167,1,7212,250,609,80,35004,12); //Hyegun Hat
	sleep 10;

	

// --------------------------------------------------
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1)
		npcshopdelitem "hatmakerhigher"+.@i,909;
	end;
}

// -------- Dummy data (duplicate as needed) --------
-	shop	hatmakerhigher1	-1,909:-1